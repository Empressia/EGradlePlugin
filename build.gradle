buildscript {
	repositories {
		maven { url(repository_url); }
	}
	dependencies {
		classpath group: "jp.empressia", name: "jp.empressia.gradle.plugin.misc", version: "1.0.0";
	}
}
plugins {
	id "java";
	id "com.gradle.plugin-publish" version "0.12.0";
	id "java-gradle-plugin";
	id "maven-publish";
}
apply plugin: "jp.empressia.gradle.plugin.misc";

ext.moduleName = "jp.empressia.gradle.plugin.misc";
ext.junitVersion = "5.7.0";
ext.hamcrestVersion = "2.2";
// for plugin setting.
//group = "jp.empressia";

repositories {
	jcenter();
	maven { url(repository_url); }
}

dependencies {
	// use Gradle API.
	implementation gradleApi();

	// use JUnit test framework.
	testImplementation(group:"org.junit.jupiter", name:"junit-jupiter-api", version:junitVersion);
	testRuntimeOnly(group:"org.junit.jupiter", name:"junit-jupiter-engine", version:junitVersion);
	testImplementation(group:"org.hamcrest", name:"hamcrest", version:hamcrestVersion);
}

tasks.withType(JavaCompile) {
	options.encoding "UTF-8";
	options.compilerArgs << "-parameters";
}

java {
	withJavadocJar();
	withSourcesJar();
}

tasks.withType(Jar) {
	archiveBaseName = moduleName;
}

javadoc {
	options.charSet "UTF-8";
	options.encoding "UTF-8";
	options.addStringOption("Xdoclint:none", "-quiet");
}

test {
	useJUnitPlatform();
}

gradlePlugin {
	plugins {
		EmpressiaPlugin  {
			id = moduleName;
			implementationClass = moduleName + "." + "Plugin";
		}
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "jp.empressia";
			artifactId = moduleName;
			artifact jar;
			artifact javadocJar;
			artifact sourcesJar;
		}
	}
	repositories {
		maven {
			url = repository_url;
		}
	}
}

pluginBundle {
	website = "https://www.empressia.jp/";
	vcsUrl = "https://github.com/Empressia/EGradlePlugin/";
	description = "Miscellaneous Tasks by Empressia.";
	tags = ["vscode", "task"];

	plugins {
		EmpressiaPlugin {
			displayName = "Empressia Gradle Plugin";
		}
	}
	mavenCoordinates {
		artifactId = moduleName;
		version = version;
	}
}
